# SPDX-License-Identifier: CC0-1.0
#
# SPDX-FileContributor: Adrian "asie" Siekierka, 2023

pkgname=wf-tools-lua
_pkgver=0.1.0
pkgver=0.1.0.r168.7308753
pkgrel=1
epoch=
pkgdesc="Wonderful Toolchain tools - platform-independent Lua scripts"
arch=(any)
url="https://github.com/WonderfulToolchain/wf-tools"
license=("MIT")
source=("$pkgname::git+https://github.com/WonderfulToolchain/wf-tools#branch=main")
depends=(wf-lua wf-lua-filesystem wf-lua-iconv wf-lua-penlight wf-lua-toml wf-pacman wf-tools-native)
sha256sums=('SKIP')
install=wf-tools-lua.install

. "/wf/config/runtime-env-vars.sh"

. "../templates/git-pkgver.PKGBUILD"

prepare() {
	cd "$pkgname"
	sed -i -e 's@/opt/wonderful@'$WF_PATH'@g' lua/*.lua lua/wf/internal/path.lua
}

package() {
	cd "$pkgname"

	# Package lua/
	cd lua
	mkdir -p "$pkgdir$WF_DESTDIR"/bin
	for i in *.lua; do
		chmod 755 "$i"
		cp -a "$i" "$pkgdir$WF_DESTDIR"/bin/"${i%.*}"
	done
	for i in wf-env*; do
		chmod 644 "$i"
		cp -a "$i" "$pkgdir$WF_DESTDIR"/bin/"$i"
	done
	mkdir -p "$pkgdir$WF_DESTDIR"/lib/lua/5.4
	cp -aR wf "$pkgdir$WF_DESTDIR"/lib/lua/5.4/wf
	cd ..
}
