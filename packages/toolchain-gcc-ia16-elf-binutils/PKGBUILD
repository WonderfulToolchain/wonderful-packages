# SPDX-License-Identifier: CC0-1.0
#
# SPDX-FileContributor: Adrian "asie" Siekierka, 2023

pkgname=toolchain-gcc-ia16-elf-binutils
_pkgver=2.43.1
pkgver=2.43.1.r119450.cb9b8d4a0ad
pkgrel=1
epoch=
pkgdesc="GNU binary utilities (IA-16 fork)"
arch=("x86_64" "aarch64")
url="https://github.com/WonderfulToolchain/binutils-ia16"
license=("GPL-3.0-or-later")
source=("$pkgname::git+https://github.com/WonderfulToolchain/binutils-ia16#branch=binutils-2_43_1-ia16")
depends=(runtime-musl)
makedepends=(runtime-musl-dev)
groups=(toolchain-gcc-ia16-elf)
sha256sums=('SKIP')

. "/wf/config/runtime-env-vars.sh"

prepare() {
	mkdir -p binutils-build
}

. "../templates/git-pkgver.PKGBUILD"

build() {
	cd binutils-build
	../"$pkgname"/configure \
		--prefix="$WF_PATH"/toolchain/gcc-ia16-elf \
		--target=ia16-elf \
		--enable-targets=ia16-elf \
		--enable-x86-hpa-segelf=yes \
		--with-bugurl="$WF_BUGURL" \
		--without-zstd \
		--enable-colored-disassembly \
		--enable-gold=no \
		--enable-ld-default \
		--enable-plugins \
		--enable-threads \
		--disable-gdb \
		--disable-gprof \
		--disable-libdecnumber \
		--disable-nls \
		--disable-shared \
		--disable-sim \
		--disable-warn-rwx-segments \
		--disable-werror
	make configure-host
	make LDFLAGS="$WF_RUNTIME_LDFLAGS"
}

package() {
	cd binutils-build
	make DESTDIR="$pkgdir" install
	cd "$pkgdir"
	wf_relocate_path_to_destdir
	rm toolchain/gcc-ia16-elf/share/info/dir
}
