# SPDX-License-Identifier: CC0-1.0
#
# SPDX-FileContributor: Adrian "asie" Siekierka, 2023

pkgname=toolchain-gcc-arm-none-eabi-picolibc-gba
_pkgver=1.8.5.1
pkgver=1.8.5.1.r24061.c0521ed7d
pkgrel=1
epoch=
pkgdesc="picolibc-based C library for Wonderful toolchain"
arch=(any)
url="https://github.com/WonderfulToolchain/wf-picolibc"
license=("BSD-3-Clause")
source=(
	"wf-picolibc::git+https://github.com/WonderfulToolchain/wf-picolibc#branch=wonderful"
	"cross-thumb.txt"
)
depends=(toolchain-gcc-arm-none-eabi-binutils toolchain-gcc-arm-none-eabi-gcc)
groups=(toolchain-gcc-arm-none-eabi)
sha256sums=(
	'SKIP'
	'SKIP'
)

. "/wf/config/runtime-env-vars.sh"

prepare() {
	mkdir -p picolibc-build
}

GIT_REPO_DIR=wf-picolibc
. "../templates/git-pkgver.PKGBUILD"

build() {
	cd picolibc-build
	export PATH=/opt/wonderful/toolchain/gcc-arm-none-eabi/bin:$PATH

	meson setup \
		--cross-file=../cross-thumb.txt \
		-Dmultilib=false \
		-Dpicocrt=false \
		-Dpicolib=false \
		-Dsemihost=false \
		-Dspecsdir=none \
		-Dtests=false \
		\
		-Dfast-bufio=true \
		-Dio-long-long=true \
		-Dio-pos-args=true \
		-Dio-percent-b=true \
		\
		-Dposix-console=true \
		-Dformat-default=long-long \
		\
		-Dnewlib-multithread=false \
		-Dnewlib-retargetable-locking=false \
		-Dthread-local-storage=false \
		-Dnewlib-nano-malloc=true \
		\
		-Dprefix="$WF_PATH"/toolchain/gcc-arm-none-eabi \
		-Dlibdir=arm-none-eabi/gba/lib \
		-Dincludedir=arm-none-eabi/gba/include \
		../wf-picolibc

	ninja
}

package() {
	cd picolibc-build
	DESTDIR="$pkgdir" ninja install

	cd "$pkgdir"
	wf_relocate_path_to_destdir
}
